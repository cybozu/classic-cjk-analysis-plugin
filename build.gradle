plugins {
    id 'java-library'
    id "com.diffplug.gradle.spotless" version "3.14.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

apply plugin: 'distribution'

version = "0.0.1"
def ElasticsearchVersion = '6.3.1'

dependencies {
    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation group: 'org.apache.lucene', name: 'lucene-analyzers-common', version: '7.4.0'
    implementation group: 'org.elasticsearch', name: 'elasticsearch', version: ElasticsearchVersion
    // Use JUnit test framework
   
    def junitJupiterVersion = '5.2.0'
    testImplementation group: 'org.junit.jupiter', name :'junit-jupiter-api', version: junitJupiterVersion
    testImplementation group: 'org.junit.jupiter', name :'junit-jupiter-engine', version: junitJupiterVersion 
    testImplementation group: 'org.junit.vintage', name :'junit-vintage-engine', version: junitJupiterVersion 
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.10.0'
    testImplementation group: 'org.apache.lucene', name: 'lucene-test-framework', version: '7.4.0'
}

test {
    useJUnitPlatform {
        includeEngines 'junit-vintage'
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

task zipPlugin(type: Zip) {
    dependsOn jar
    archiveName = "${project.name}-${version}-es-${ElasticsearchVersion}.zip"
    from 'src/main/dist', "$buildDir/libs"

    destinationDir = file("$buildDir/dist") 
}

spotless {
    java {
        importOrder('' /** All Other imports */,  
                     '\n', 
                     'java', 
                     '\n', 
                     'net', 
                     '\n', 
                     'javax', 
                     '\n', 
                     'junit', 
                     'lombok', 
                     'mockit', 
                     '\n', 
                     'org', 
                     '\n', 
                     'au', 
                     '\n', 
                     'com', 
                     '\n', 
                     'static '/** All Other static imports */) 
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
        eclipse().configFile 'etc/eclipse-formatter-config.xml'
    }
}
